# Copyright (C) 2024 Cadwork
# All rights reserved.

CMAKE_MINIMUM_REQUIRED(VERSION 3.21.1)

PROJECT(CwAPI3D VERSION 30.573.1 LANGUAGES CXX C)

SET(MAIN_PROJECT OFF)
IF(CMAKE_CURRENT_SOURCE_DIR STREQUAL CMAKE_SOURCE_DIR)
  SET(MAIN_PROJECT ON)
ENDIF()

SET(CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/cmake ${CMAKE_MODULE_PATH})

INCLUDE(GNUInstallDirs)

SET(CWAPI3D_INCLUDE_INSTALL_DIR "${CMAKE_INSTALL_INCLUDEDIR}")
SET(CWAPI3D_CMAKE_CONFIG_DIR "${CMAKE_CURRENT_BINARY_DIR}")
SET(CWAPI3D_INCLUDE_BUILD_DIR "${PROJECT_SOURCE_DIR}/include/")

ADD_LIBRARY(${PROJECT_NAME} INTERFACE)
ADD_LIBRARY(${PROJECT_NAME}::${PROJECT_NAME} ALIAS ${PROJECT_NAME})

TARGET_INCLUDE_DIRECTORIES(${PROJECT_NAME} INTERFACE
                           $<BUILD_INTERFACE:${CWAPI3D_INCLUDE_BUILD_DIR}>
                           $<INSTALL_INTERFACE:${CWAPI3D_INCLUDE_INSTALL_DIR}>)

INCLUDE(CMakePackageConfigHelpers)

CONFIGURE_FILE("cmake/${PROJECT_NAME}ConfigVersion.cmake.in"
               "${CWAPI3D_CMAKE_CONFIG_DIR}/${PROJECT_NAME}ConfigVersion.cmake"
               @ONLY)

CONFIGURE_FILE("cmake/config.cmake.in"
               "${CWAPI3D_CMAKE_CONFIG_DIR}/${PROJECT_NAME}Config.cmake"
               @ONLY)

INSTALL(DIRECTORY "${PROJECT_SOURCE_DIR}/include/"
        DESTINATION ${CWAPI3D_INCLUDE_INSTALL_DIR})

INSTALL(FILES "${CWAPI3D_CMAKE_CONFIG_DIR}/${PROJECT_NAME}Config.cmake" "${CWAPI3D_CMAKE_CONFIG_DIR}/${PROJECT_NAME}ConfigVersion.cmake"
        DESTINATION "${CMAKE_INSTALL_DATADIR}/cmake/${PROJECT_NAME}")

EXPORT(TARGETS ${PROJECT_NAME}
       NAMESPACE ${PROJECT_NAME}::
       FILE "${CWAPI3D_CMAKE_CONFIG_DIR}/${PROJECT_NAME}Targets.cmake")

INSTALL(TARGETS ${PROJECT_NAME}
        EXPORT "${PROJECT_NAME}Targets"
        INCLUDES DESTINATION ${CWAPI3D_INCLUDE_INSTALL_DIR})

INSTALL(EXPORT "${PROJECT_NAME}Targets"
        NAMESPACE ${PROJECT_NAME}::
        DESTINATION "${CMAKE_INSTALL_DATADIR}/cmake/${PROJECT_NAME}")
