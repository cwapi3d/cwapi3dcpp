# Copyright (C) 2024 Cadwork
# All rights reserved.

CMAKE_MINIMUM_REQUIRED(VERSION 3.21.1)

# ------------------------------------------------------------------------------
# Start CwAPI3D Project
# ------------------------------------------------------------------------------
PROJECT(CwAPI3D VERSION 30.10.1 LANGUAGES CXX C)

SET(CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/cmake ${CMAKE_MODULE_PATH})

INCLUDE(GNUInstallDirs)

SET(CWAPI3D_CONFIG_INSTALL_DIR "${CMAKE_INSTALL_DATADIR}/cmake/${PROJECT_NAME}" CACHE INTERNAL "")
SET(CWAPI3D_INCLUDE_INSTALL_DIR "${CMAKE_INSTALL_INCLUDEDIR}")
SET(CWAPI3D_TARGETS_EXPORT_NAME "${PROJECT_NAME}Targets")
SET(CWAPI3D_CMAKE_CONFIG_TEMPLATE "cmake/config.cmake.in")
SET(CWAPI3D_CMAKE_CONFIG_DIR "${CMAKE_CURRENT_BINARY_DIR}")
SET(CWAPI3D_CMAKE_VERSION_CONFIG_FILE "${CWAPI3D_CMAKE_CONFIG_DIR}/${PROJECT_NAME}ConfigVersion.cmake")
SET(CWAPI3D_CMAKE_PROJECT_CONFIG_FILE "${CWAPI3D_CMAKE_CONFIG_DIR}/${PROJECT_NAME}Config.cmake")
SET(CWAPI3D_CMAKE_PROJECT_TARGETS_FILE "${CWAPI3D_CMAKE_CONFIG_DIR}/${PROJECT_NAME}Targets.cmake")
SET(CWAPI3D_INCLUDE_BUILD_DIR "${PROJECT_SOURCE_DIR}/include/")

# ------------------------------------------------------------------------------
# Create Interface Library
# ------------------------------------------------------------------------------
ADD_LIBRARY(${PROJECT_NAME} INTERFACE)
ADD_LIBRARY(${PROJECT_NAME}::${PROJECT_NAME} ALIAS ${PROJECT_NAME})

# ------------------------------------------------------------------------------
# Specify Include Directories
# ------------------------------------------------------------------------------
TARGET_INCLUDE_DIRECTORIES(${PROJECT_NAME} INTERFACE
                           $<BUILD_INTERFACE:${CWAPI3D_INCLUDE_BUILD_DIR}>
                           $<INSTALL_INTERFACE:${CWAPI3D_INCLUDE_INSTALL_DIR}>)

INCLUDE(CMakePackageConfigHelpers)

CONFIGURE_FILE("cmake/${PROJECT_NAME}ConfigVersion.cmake.in"
               ${CWAPI3D_CMAKE_VERSION_CONFIG_FILE}
               @ONLY)

CONFIGURE_FILE(${CWAPI3D_CMAKE_CONFIG_TEMPLATE}
               ${CWAPI3D_CMAKE_PROJECT_CONFIG_FILE}
               @ONLY)

INSTALL(DIRECTORY ${CWAPI3D_INCLUDE_BUILD_DIR}
        DESTINATION ${CWAPI3D_INCLUDE_INSTALL_DIR})

INSTALL(FILES ${CWAPI3D_CMAKE_PROJECT_CONFIG_FILE} ${CWAPI3D_CMAKE_VERSION_CONFIG_FILE}
        DESTINATION ${CWAPI3D_CONFIG_INSTALL_DIR})

EXPORT(TARGETS ${PROJECT_NAME}
       NAMESPACE ${PROJECT_NAME}::
       FILE ${CWAPI3D_CMAKE_PROJECT_TARGETS_FILE})

INSTALL(TARGETS ${PROJECT_NAME}
        EXPORT ${CWAPI3D_TARGETS_EXPORT_NAME}
        INCLUDES DESTINATION ${CWAPI3D_INCLUDE_INSTALL_DIR})

INSTALL(EXPORT ${CWAPI3D_TARGETS_EXPORT_NAME}
        NAMESPACE ${PROJECT_NAME}::
        DESTINATION ${CWAPI3D_CONFIG_INSTALL_DIR})
